package suspiciousBehaviourCEP
import com.sbnz.model.AuthInfoWithClientIP
import com.sbnz.model.SecurityResponseDTO


declare window LatestAuth
    AuthInfoWithClientIP() over window:length(1)
end

declare window TimeAuth
    AuthInfoWithClientIP() over window:time(45s)
end



rule "Too many login attempts with same email"
salience($total)
    when
        $srd: SecurityResponseDTO();
        $email1: AuthInfoWithClientIP($email: email, checked == false, login == true) from window LatestAuth
        $total: Number(intValue >= 3) from accumulate(
            $email2: AuthInfoWithClientIP(
                email.equals($email)
            ) from window TimeAuth,
            count($email2)
        )
    then
        System.out.println("Too many login attempts with same email");
        $srd.setMessage("Too many login attempts with same email");
        $srd.setCode(400);
        modify($email1) {setChecked(true);}
    end


rule "Too many login attempts from same machine"
salience($total)
    when
        $srd: SecurityResponseDTO();
        $ip1: AuthInfoWithClientIP($ip: ip, checked == false, login == true) from window LatestAuth
        $total: Number(intValue >= 3) from accumulate(
            $ip2: AuthInfoWithClientIP(
                ip.equals($ip)
            ) from window TimeAuth,
            count($ip2)
        )
    then
        System.out.println("Too many login attempts from same machine");
        $srd.setMessage("Too many login attempts from same machine");
        $srd.setCode(401);
        modify($ip1) {setChecked(true);}
    end


rule "Too many registration attempts from same machine - captcha"
salience(4)
    when
        $srd: SecurityResponseDTO();
        $ip1: AuthInfoWithClientIP($ip: ip, checked == false, login == false, captcha == false) from window LatestAuth
        $total: Number(intValue >= 2) from accumulate(
            $ip2: AuthInfoWithClientIP(
                ip.equals($ip)
            ) from window TimeAuth,
            count($ip2)
        )
    then
        System.out.println("Too many registration attempts from same machine - captcha");
        $srd.setMessage("Too many registration attempts from same machine - captcha");
        $srd.setCode(402);
        modify($ip1) {setChecked(true);}
    end

rule "Too many registration attempts from same machine - forbid further registrations"
salience($total)
    when
        $srd: SecurityResponseDTO();
        $ip1: AuthInfoWithClientIP($ip: ip, checked == false, login == false) from window LatestAuth
        $total: Number(intValue > 4) from accumulate(
            $ip2: AuthInfoWithClientIP(
                ip.equals($ip)
            ) from window TimeAuth,
            count($ip2)
        )
    then
        System.out.println("Too many registration attempts from same machine - forbid further registrations");
        $srd.setMessage("Too many registration attempts from same machine - forbid further registrations");
        $srd.setCode(502);
        modify($ip1) {setChecked(true);}
    end